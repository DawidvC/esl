/*
 * Unbuffered Formatted Output
 * Assumes target wordsize is 32-bits or more.
 *
 * Copyright (c) 2009-2017, Brian G. Lucas.  See LICENSE file.
 */

import sys;

package out
{
    type OutFD: sys.fildes;

    proc (o: OutFD) nl()
    {
	sys.write(o, "\n", 1);
    }

    proc (o: OutFD) chr(c: _byte)
    {   var buf: [1]_byte;

	buf[0] = c;
	sys.write(o, buf, 1);
    }

    proc (o: OutFD) str(s:@[]_byte)
    {   var n: _uint;

	n = _zlen(s@);
	sys.write(o, s, n);
    }

    proc (o: OutFD) strn(s:@[]_byte, len:_uint)
    {
	sys.write(o, s, len);
    }

    const hexchr: [16]_byte =
	{'0','1','2','3','4','5','6','7','8','9','A','B','C','D','E','F'};

    proc (o: OutFD) hex8(in: _uint8)
    {   var buf: [2]_byte;

	buf[0] = hexchr[(in>>4)&0xF];
	buf[1] = hexchr[in&0xF];
	sys.write(o, buf, 2);
    }

    proc (o: OutFD) hex16(in:_uint16)
    {   var buf: [4]_byte;

	buf[0] = hexchr[(in>>12)&0xF];
	buf[1] = hexchr[(in>>8)&0xF];
	buf[2] = hexchr[(in>>4)&0xF];
	buf[3] = hexchr[in&0xF];
	sys.write(o, buf, 4);
    }

    proc (o: OutFD) hex32(in:_uint32)
    {	var buf: [8]_byte;
	var i: _uint;

	i = 7;
	loop
	{   buf[i] = hexchr[in&0xF];
	  exit i == 0;
	    i -= 1;
	    in = in >> 4;
	}
	sys.write(o, buf, 8);
    }

    proc (o: OutFD) hex64(in: _uint64)
    {   var buf: [16]_byte;
	var i: 0..15;

	i = 15;
	loop
	{   buf[i] = hexchr[_uint(in)&0xF];
	  exit i == 0;
	    i -= 1;
	    in = in >> 4;
	}
	sys.write(o, buf, 16);
    }  

    proc (o: OutFD) hex(in: _uint):: inline
    {
	if _uint?size == _uint64?size then
	    o.hex64(in);
	else
	    o.hex32(_uint32(in));
    }

    proc(o: OutFD)  uint64(in: _uint64)
    {	var i: _uint;
	var buf: [20]_byte;

	i = 20;
	if in == 0 then
	{   i -= 1;
	    buf[i] = '0';
	}
	else
	{   while in != 0 do
	    {   i -= 1;
		buf[i] = _byte((in % 10) + '0');
		in = in / 10;
	    }
	}
	sys.write(o, buf[i:], 20-i);
    }

    proc(o: OutFD)  uint32(in: _uint32)
    {	var i: _uint;
	var buf: [10]_byte;

	if _uint?size == _uint64?size then  // extend to native
	    o.uint64(_uint64(in));
	else
	{   i = 10;
	    if in == 0 then
	    {   i -= 1;
		buf[i] = '0';
	    }
	    else
	    {   while in != 0 do
		{   i -= 1;
		    buf[i] = _byte((in % 10) + '0');
		    in = in / 10;
		}
	    }
	    sys.write(o, buf[i:], 10-i);
 	}
    }

    proc (o: OutFD) uint16(in: _uint16)
    {
	if _uint?size == _uint64?size then  // extend to native
	    o.uint64(_uint64(in));
	else
	    o.uint32(_uint32(in));
    }

    proc (o: OutFD) uint8(in: _uint8)
    {
	if _uint?size == _uint64?size then  // extend to native
	    o.uint64(_uint64(in));
	else
	    o.uint32(_uint32(in));
   }

    proc (o: OutFD) uint(in: _uint):: inline
    {
	if _uint?size == _uint64?size then
	    o.uint64(in);
	else
	    o.uint32(_uint32(in));
    }

    proc (o: OutFD) int64(in: _int64)
    {	var ui: _uint64;

	if in < 0 then
	{   sys.write(o, "-", 1);
	    ui = _uint64(-in);
	}
	else
	    ui = _uint64(in);
	o.uint64(ui);
    }	

    proc (o: OutFD) int32(in: _int32)
    {	var ui: _uint32;

	if _int?size == _int64?size then  // extend to native
	    o.int64(_int64(in));
	else
	{   if in < 0 then
	    {   sys.write(o, "-", 1);
		ui = _uint64(-in);
	    }
	    else
		ui = _uint64(in);
	    o.uint32(ui);
	}
    }	

    proc (o: OutFD) int16(in: _int16)
    {
	if _int?size == _int64?size then  // extend to native
	    o.int64(_int64(in));
	else
	    o.int32(_int32(in));
    }

    proc (o: OutFD) int8(in: _int8)
    {
	if _int?size == _int64?size then  // extend to native
	    o.int64(_int64(in));
	else
	    o.int32(_int32(in));
    }

    proc (o: OutFD) int(in: _int):: inline
    {
	if _int?size == _int64?size then
	    o.int64(in);
	else
	    o.int32(_int32(in));
    }

    /*
     * Fixed point output using only integers
     */
    proc (o: OutFD) ufix16(x: _uint16, prec: _uint, bp: _uint)
    {   var w: _uint32;	// _ufix(32,16)

	w = _uint32(x);		// zero extend
	if bp < 16 then
	{   w = w << (16-bp);	// shift binary point between half-words
	    o.uint16(_uint16(w >> 16));
	}
	else
	    o.chr('0');
	o.chr('.');
	while prec > 0 do
	{   x = _uint16(w);	// keep lo half
	    w = x * 10;		// hi half has value 0..9
	    o.chr('0'+ (w >> 16));
	    prec -= 1;
	}
    }

    proc (o: OutFD) sfix16(x: _int16, prec: _uint, bp: _uint)
    {
	if x < 0 then
	{   o.chr('-');
	    x = -x;		// FIXME - check for x?min
	}
	o.ufix16(_uint16(x), prec, bp);
    }

    proc (o: OutFD) ufix32(x: _uint32, prec: _uint, bp: _uint)
    {   var w: _uint64;	// _ufix(64,32)

	w = _uint64(x);		// zero extend
	if bp < 32 then
	{   w = w << (32-bp);	// shift binary point between words
	    o.uint32(_uint32(w >> 32));
	}
	else
	    o.chr('0');
	o.chr('.');
	while prec > 0 do
	{   x = _uint32(w);	// keep lo half
	    w = x * 10;		// hi half has value 0..9
	    o.chr('0'+ (w >> 32));
	    prec -= 1;
	}
    }

    proc (o: OutFD) sfix32(x: _int32, prec: _uint, bp: _uint)
    {
	if x < 0 then
	{   o.chr('-');
	    x = -x;		// FIXME - check for x?min
	}
	o.ufix32(_uint32(x), prec, bp);
    }

    proc (o: OutFD) ufix64(x: _uint64, prec: _uint, bp: _uint)
    {   var w: _uint128;

	w = _uint128(x);
	if bp < 64 then
	{   o.uint64(_uint32(x >> bp));
	    w = x << (64-bp);	// shift binary point to top
	}
	else
	    o.chr('0');
	o.chr('.');
	while prec > 0 do
	{   x = _uint64(w);	// keep lo half
	    w = x * 10;		// hi half has value 0..9
	    o.chr('0'+ (w >> 64));
	    prec -= 1;
	}
    }

    proc (o: OutFD) sfix64(x: _int64, prec: _uint, bp: _uint)
    {
	if x < 0 then
	{   o.chr('-');
	    x = -x;		// FIXME - check for x?min
	}
	o.ufix32(_uint64(x), prec, bp);
    }

}
// vim: ts=8 sw=4 noet nowrap

